/*
 * Copyright (C) 2017 Gabriel Pulido
 *
 * This file is part of Archery Annotation.
 *
 *  Archery Annotation is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License version 2
 *  as published by the Free Software Foundation.
 *
 *  Archery Annotation is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'realm-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.gpulido'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    publishNonDefault true

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 8
        versionName "0.0.8"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${versions.supportLib}"
    implementation "com.google.code.gson:gson:${versions.gson}"
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation group: 'com.fasterxml.uuid', name: 'java-uuid-generator', version: '3.1.4'
    implementation "com.annimon:stream:${versions.annimonStreams}"
    implementation 'net.objecthunter:exp4j:0.4.8'
    implementation 'com.jakewharton.hugo:hugo-annotations:1.2.1'
    implementation "com.android.support:percent:${versions.supportLib}"
    annotationProcessor 'dk.ilios:realmfieldnameshelper:1.1.1'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


realm {
    syncEnabled = true
}
